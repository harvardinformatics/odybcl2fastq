'''
snakemake workflow for 10x single cell

Created on  2019-03-25

@author: Meghan Correa <mportermahoney@g.harvard.edu>
@copyright: 2019 The Presidents and Fellows of Harvard College. All rights reserved.
@license: GPL v2.0
'''

configfile: "snakemake_config.json"
localrules: all, update_lims_db, cp_source_to_output, checksum, publish, demultiplex_10x_cmd, count_10x_cmd, fastqc_cmd
from odybcl2fastq.run import update_lims_db, setup_run_logger
from odybcl2fastq.parsers.samplesheet import SampleSheet
from odybcl2fastq.emailbuilder.emailbuilder import buildmessage
from odybcl2fastq import config as ody_config

onstart:
    """
    touch processed file to prevent reprocessing
    prepare log and script dirs
    """
    shell("touch {source}{run}/ody.processed", source=config['source'], run=config['run'])
    shell("mkdir -p {output}{run}", output={config['output']}, run=config['run'])
    shell("mkdir -p {output}{run}/log", output={config['output']}, run=config['run'])
    shell("mkdir -p {output}{run}/script", output={config['output']}, run=config['run'])

rule all:
    """
    final output of workflow
    """
    input:
        expand("{source}{run}/ody.complete", source=config['source'], run=config['run'])
        #expand("{output}{run}/script/demultiplex_10x.sh", output=config['output'], run=config['run'])

rule demultiplex_10x_cmd:
    """
    build a bash file with the demux cmd
    """
    input:
        run_dir=expand("{source}{{run}}/SampleSheet.csv", source=config['source'])
    output:
        expand("{output}{{run}}/script/demultiplex_10x.sh", output=config['output'])
    shell:
        """
        cmd="#!/bin/bash\n"
        cmd+="source new-modules.sh\n"
        cmd+="module purge\n"
        cmd+="module load cellranger/3.0.1-fasrc01\n\n"
        cmd+="cellranger mkfastq --localcores=8 --ignore-dual-index --run={config[source]}{wildcards.run} --samplesheet={input.run_dir} --output-dir={config[output]}{wildcards.run}/fastq"
        echo "$cmd" >> {output}
        chmod 777 {output}
        """

rule demultiplex_10x:
    """
    run bash file for demux
    the slurm_submit.py script will add slurm params to the top of this file
    """
    input:
        expand("{output}{{run}}/script/demultiplex_10x.sh", output=config['output'])
    output:
        touch(expand("{output}{{run}}/demultiplex.processed", output=config['output']))
    shell:
        """
        {input}
        """

# DONT run count for now since we don't have a way to detect reference genome
rule count_10x_cmd:
    """
    build a bash file with the demux cmd
    """
    input:
        #expand("{output}{{run}}/demultiplex.processed", output=config['output'])
        expand("{source}{{run}}/ody.complete", source=config['source'])
    output:
        expand("{output}{{run}}/script/count.sh", output=config['output'])
    shell:
        """
        cmd="#!/bin/bash\n"
        cmd+="source new-modules.sh\n"
        cmd+="module purge\n"
        cmd+="module load cellranger/3.0.1-fasrc01\n\n"
        cmd+="cellranger count --lanes=1,2,3,4 --id={config[sample]} --transcriptome=/n/boslfs/LABS/informatics/refs/10x/refdata-cellranger-GRCh38-1.2.0 --localcores=12 --sample={config[sample]} --fastqs={config[output]}{wildcards.run}/fastq/{config[sample]}"
        echo "$cmd" >> {output}
        chmod 777 {output}
        """

rule count_10x:
    """
    run bash file for count
    the slurm_submit.py script will add slurm params to the top of this file
    """
    input:
        script=expand("{output}{{run}}/script/count.sh", output=config['output'])
    output:
        touch(expand("{output}{{run}}/count.processed", output=config['output']))
    shell:
        """
        {input}
        """

rule update_lims_db:
    """
    connect the submission with the run in lims
    """
    input:
        expand("{output}{{run}}/demultiplex.processed", output=config['output']),
        sample_sheet=expand("{source}{{run}}/SampleSheet.csv", source=config['source']),
        run_folder=expand("{source}{{run}}", source=config['source'])
    output:
        touch(expand("{output}{{run}}/update_lims_db.processed", output=config['output']))
    run:
        # TODO: use this setup_logger for any logging from ody python functions
        setup_run_logger(wildcards.run, False)
        sample_sheet = SampleSheet(input.sample_sheet[0])
        instrument = sample_sheet.get_instrument()
        update_lims_db(input.run_folder[0], sample_sheet.sections, instrument)

rule fastqc_cmd:
    """
    build a bash file with the fastqc cmd
    """
    input:
        ancient(expand("{output}{{run}}/demultiplex.processed", output=config['output']))
    output:
        expand("{output}{{run}}/script/fastqc.sh", output=config['output'])
    shell:
        """
        mkdir -p {config[output]}{wildcards.run}/QC
        cmd="#!/bin/bash\n"
        cmd+="source new-modules.sh\n"
        cmd+="module purge\n"
        cmd+="module load fastqc/0.11.5-fasrc01\n\n"
        cmd+="mkdir -p {config[output]}{wildcards.run}/QC\n"
        cmd+="fastqc -o {config[output]}{wildcards.run}/QC --threads 1 -b $(find {config[output]}{wildcards.run}/fastq/ -name *.fastq.gz -not -name Undetermined* -print0 | xargs -0)"
        echo "$cmd" >> {output}
        chmod 777 {output}
        """

rule fastqc:
    """
    run bash file for fastqc
    the slurm_submit.py script will add slurm params to the top of this file
    """
    input:
        expand("{output}{{run}}/script/fastqc.sh", output=config['output'])
    output:
        touch(expand("{output}{{run}}/fastqc.processed", output=config['output']))
    shell:
        """
        {input}
        """

rule cp_source_to_output:
    """
    copy a few files from source to output dir
    """
    input:
        expand("{output}{{run}}/demultiplex.processed", output=config['output'])
    params:
        sample_sheet="SampleSheet.csv",
        run_info="RunInfo.xml",
        interop="InterOp",
        nextseq_run_params="RunParameters.xml",
        hiseq_run_params="runParameters.xml"
    output:
        sample_sheet=expand("{output}{{run}}/SampleSheet.csv", output=config['output']),
        run_info=expand("{output}{{run}}/RunInfo.xml", output=config['output']),
        interop=directory(expand("{output}{{run}}/InterOp", output=config['output']))
    shell:
        """
        cp {config[source]}{wildcards.run}/{params.sample_sheet} {output.sample_sheet}
        cp {config[source]}{wildcards.run}/{params.run_info} {output.run_info}
        cp -r {config[source]}{wildcards.run}/{params.interop} {output.interop}
        # copy these if they exist
        cp {config[source]}{wildcards.run}/{params.nextseq_run_params} {config[output]}{wildcards.run}/{params.nextseq_run_params} 2>/dev/null || :
        cp {config[source]}{wildcards.run}/{params.hiseq_run_params} {config[output]}{wildcards.run}/{params.hiseq_run_params} 2>/dev/null || :

        """

rule checksum:
    """
    calculate checksum for all the fastq files
    """
    input:
        expand("{output}{{run}}/SampleSheet.csv", output=config['output']),
        expand("{output}{{run}}/RunInfo.xml", output=config['output'])
    output:
        checksum=expand("{output}{{run}}/md5sum.txt", output=config['output']),
    shell:
        """
        files=$(find {config[output]}{wildcards.run}/ -name *.fastq.gz -print0 | xargs -0)
        md5sum $files > {output.checksum}
        """

rule publish:
    """
    copy all output to a published location
    """
    input:
        checksum=expand("{output}{{run}}/md5sum.txt", output=config['output']),
        fastqc=expand("{output}{{run}}/fastqc.processed", output=config['output']),
        lims=expand("{output}{{run}}/update_lims_db.processed", output=config['output'])
    output:
        touch(expand("{source}{{run}}/ody.complete", source=config['source']))
    shell:
        """
        cp -r {config[output]}{wildcards.run} {config[published]}{wildcards.run}
        """

onsuccess:
    message = 'run %s completed successfully\n see logs here: %s%s.log\n' % (config['run'], config['log'], config['run'])
    subject = 'Demultiplex Summary for: %s' % config['run']
    sent = buildmessage(message, subject, {}, ody_config.EMAIL['from_email'], ody_config.EMAIL['to_email'])

onerror:
    message = 'run %s failed\n see logs here: %s%s.log\n' % (config['run'], config['log'], config['run'])
    subject = 'Run Failed: %s' % config['run']
    sent = buildmessage(message, subject, {}, ody_config.EMAIL['from_email'], ody_config.EMAIL['to_email'])
